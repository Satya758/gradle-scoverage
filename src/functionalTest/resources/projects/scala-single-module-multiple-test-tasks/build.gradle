plugins {
    id 'io.spring.dependency-management' version "1.0.4.RELEASE"
    id 'org.scoverage'
    id 'jvm-test-suite'
}

repositories {
    mavenCentral()
}

description = 'a single-module Scala project with dependency manager that builds successfully with 100% coverage'

apply plugin: 'scala'


dependencyManagement {
    dependencies {
        dependency group: 'org.scala-lang', name: 'scala-library', version: "${scalaVersionMajor}.${scalaVersionMinor}.${scalaVersionBuild}"
    }
}

dependencies {
    implementation group: 'org.scala-lang', name: 'scala-library'

    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: junitVersion
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion

    testImplementation group: 'org.scalatest', name: "scalatest_${scalaVersionMajor}.${scalaVersionMinor}", version: scalatestVersion
}

testing {
    suites {
        configureEach {
            useJUnit()
            targets.configureEach {
                testTask.configure {
                    maxParallelForks = 1
                }
            }
        }
        intTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            // dependencies { ... } does not appear to work as advertised?
            sources {
                scala {
                    compileClasspath += sourceSets.test.compileClasspath + sourceSets.main.output + sourceSets.test.output
                    runtimeClasspath += sourceSets.test.runtimeClasspath
                }
            }
            targets.configureEach {
                testTask.configure{
                    outputs.upToDateWhen { false }
                    mustRunAfter(test)
                }
            }
        }
    }
}
check.dependsOn(testing.suites.intTest)

scoverage {
    minimumRate = 0.6
}

if (hasProperty("excludedFile")) {
    scoverage.excludedFiles = [excludedFile]
}
