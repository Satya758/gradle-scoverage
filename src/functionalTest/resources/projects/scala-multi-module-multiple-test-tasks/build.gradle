plugins {
    id 'org.scoverage' apply false
}

allprojects {
    repositories {
        mavenCentral()
    }
}

description = 'a multi-module Scala project that builds successfully with 100% coverage'

allprojects {

    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'org.scoverage'

    dependencies {
        implementation group: 'org.scala-lang', name: 'scala-library', version: "${scalaVersionMajor}.${scalaVersionMinor}.${scalaVersionBuild}"

        testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: junitVersion
        testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion

        testImplementation group: 'org.scalatest', name: "scalatest_${scalaVersionMajor}.${scalaVersionMinor}", version: scalatestVersion
    }

    testing {
        suites {
            configureEach {
                useJUnit()
                targets.configureEach {
                    testTask.configure {
                        maxParallelForks = 1
                    }
                }
            }
            intTest(JvmTestSuite) {
                testType = TestSuiteType.INTEGRATION_TEST
                // dependencies { ... } does not appear to work as advertised?
                sources {
                    scala {
                        compileClasspath += sourceSets.test.compileClasspath + sourceSets.main.output + sourceSets.test.output
                        runtimeClasspath += sourceSets.test.runtimeClasspath
                    }
                }
                targets.configureEach {
                    testTask.configure{
                        outputs.upToDateWhen { false }
                        mustRunAfter(test)
                    }
                }
            }
        }
    }
    check.dependsOn(testing.suites.intTest)

    scoverage {
        minimumRate = 0.5
    }
}
